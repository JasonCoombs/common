#
#
# ***********************************************************************************
# * Copyright (C) 2018 - 2020, BlockSettle AB
# * Distributed under the GNU Affero General Public License (AGPL v3)
# * See LICENSE or http://www.gnu.org/licenses/agpl.html
# *
# **********************************************************************************
#
#
CMAKE_MINIMUM_REQUIRED(VERSION 3.3)

PROJECT(${BS_PROTO_LIB_NAME})

SET(GENERATED_SOURCE_FILES "")
SET(GENERATED_HEADER_FILES "")
SET(GENERATED_INCLUDE_DIRS "")

FUNCTION(GENERATE_PROTO PROTO_DIR)
   SET(IN_DIR ${PROTO_ROOT_DIR}/common/Blocksettle_proto/${PROTO_DIR})
   SET(OUT_DIR ${PATH_TO_GENERATED}/BS/${PROTO_DIR})

   FILE(MAKE_DIRECTORY ${OUT_DIR})

   FILE(GLOB PROTO_FILES ${IN_DIR}/*.proto)

   FOREACH(PROTO_FILE ${PROTO_FILES})
      GET_FILENAME_COMPONENT(PROTO_NAME "${PROTO_FILE}" NAME_WE)

      SET(PROTO_SOURCE_FILE "${OUT_DIR}/${PROTO_NAME}.pb.cc")
      SET(PROTO_HEADER_FILE "${OUT_DIR}/${PROTO_NAME}.pb.h")

      SET(PROTO_SOURCE_FILES ${PROTO_SOURCE_FILES} ${PROTO_SOURCE_FILE})
      SET(PROTO_HEADER_FILES ${PROTO_HEADER_FILES} ${PROTO_HEADER_FILE})

      ADD_CUSTOM_COMMAND(OUTPUT ${PROTO_SOURCE_FILE}
                         OUTPUT ${PROTO_HEADER_FILE}
                         DEPENDS ${PROTO_FILE}
                         COMMAND ${PROTOBUF_PROTOC_EXECUTABLE} ${PROTO_FILE} --cpp_out=${OUT_DIR} --proto_path=${IN_DIR} --proto_path=${PROTO_ROOT_DIR}/common/Blocksettle_proto
      )
   ENDFOREACH(PROTO_FILE)

   SET(GENERATED_SOURCE_FILES ${GENERATED_SOURCE_FILES} ${PROTO_SOURCE_FILES} PARENT_SCOPE)
   SET(GENERATED_HEADER_FILES ${GENERATED_HEADER_FILES} ${PROTO_HEADER_FILES} PARENT_SCOPE)

   SET(GENERATED_INCLUDE_DIRS ${GENERATED_INCLUDE_DIRS} ${OUT_DIR} PARENT_SCOPE)

ENDFUNCTION(GENERATE_PROTO)

GENERATE_PROTO(.)
GENERATE_PROTO(Communication)
GENERATE_PROTO(Storage)
GENERATE_PROTO("BS_MD/TradeHistoryServer")

ADD_LIBRARY(${BS_PROTO_LIB_NAME} ${GENERATED_SOURCE_FILES} ${GENERATED_HEADER_FILES})

TARGET_INCLUDE_DIRECTORIES(${BS_PROTO_LIB_NAME} PUBLIC ${GENERATED_INCLUDE_DIRS})

TARGET_LINK_LIBRARIES(${BS_PROTO_LIB_NAME}
   ${PROTO_LIB}
)
